svg-path =
    wsp* moveto-drawto-command-groups? wsp*
moveto-drawto-command-groups =
    moveto-drawto-command-group (wsp* moveto-drawto-command-groups)*
moveto-drawto-command-group =
    moveto wsp* (drawto-command (wsp* drawto-commands)*)?
drawto-command =
    closepath
    | lineto
    | horizontal-lineto
    | vertical-lineto
    | curveto
    | smooth-curveto
    | quadratic-bezier-curveto
    | smooth-quadratic-bezier-curveto
    | elliptical-arc
moveto =
    ( "M" | "m" ) wsp* coordinate-pair (comma-wsp? lineto-argument-sequence)?
closepath =
    ("Z" | "z")
lineto =
    ( "L" | "l" ) wsp* lineto-argument-sequence
lineto-argument-sequence =
    coordinate-pair (comma-wsp? coordinate-pair)*
horizontal-lineto =
    ( "H" | "h" ) wsp* coordinate (comma-wsp? coordinate)*
vertical-lineto =
    ( "V" | "v" ) wsp* coordinate (comma-wsp? coordinate)*
curveto =
    ( "C" | "c" ) wsp* curveto-argument (comma-wsp? curveto-argument)*
curveto-argument =
    coordinate-pair comma-wsp? coordinate-pair comma-wsp? coordinate-pair
smooth-curveto =
    ( "S" | "s" ) wsp* smooth-curveto-argument (comma-wsp? smooth-curveto-argument)*
smooth-curveto-argument =
    coordinate-pair comma-wsp? coordinate-pair
quadratic-bezier-curveto =
    ( "Q" | "q" ) wsp* quadratic-bezier-curveto-argument (comma-wsp? quadratic-bezier-curveto-argument)*
quadratic-bezier-curveto-argument =
    coordinate-pair comma-wsp? coordinate-pair
smooth-quadratic-bezier-curveto =
    ( "T" | "t" ) wsp* coordinate-pair (comma-wsp? coordinate-pair)*
elliptical-arc =
    ( "A" | "a" ) wsp* elliptical-arc-argument (comma-wsp? elliptical-arc-argument)*
elliptical-arc-argument =
    nonnegative-number comma-wsp? nonnegative-number comma-wsp?
        number comma-wsp flag comma-wsp? flag comma-wsp? coordinate-pair
coordinate-pair =
    coordinate comma-wsp? coordinate
coordinate =
    number
nonnegative-number =
    integer-constant
    | floating-point-constant
number =
    sign? (integer-constant | floating-point-constant)
flag =
    "0" | "1"
comma-wsp =
    ((wsp+ comma?) | comma)  wsp*
comma =
    ","
integer-constant =
    digit-sequence
floating-point-constant =
    fractional-constant exponent?
    | digit-sequence exponent
fractional-constant =
    digit-sequence? "." digit-sequence
    | digit-sequence "."
exponent =
    ( "e" | "E" ) sign? digit-sequence
sign =
    "+" | "-"
digit-sequence =
    digit+
digit =
    "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
wsp =
    (#x20 | #x9 | #xD | #xA)

https://www.w3.org/TR/SVG2/paths.html#PathDataBNF
